$include_const 'iglobals.pasm';
$include_const 'libpaths.pasm';

function main[main](var argv) {
  // ignore argv[0]
  argv.shift(); 

  var opts  = getGetopt().get_options(argv);
  var files = argv;

  if (opts['help']
  || opts['usage']
  || elements(files) == 0)
    usage_exit();


  string outfile;
  int    execute;
  if (opts['output'] != null) {
    outfile = opts['output'];
    execute = false;
  } else {
    outfile = '__ometa_winxed__eval__.winxed';
    execute = true;
  }

  if (opts['lib-path']) {
    var newpath = opts['lib-path'];
    var libpath = getinterp()[IGLOBALS_LIB_PATHS][PARROT_LIB_PATH_LIBRARY];
    ${ push libpath, newpath };
  }

  using extern OMetaWinxed;
  using extern OMetaWinxed.Compiler;

  var outh = new FileHandle;
  outh.open(outfile, 'w');
  outh.print('function __load__[anon, load, init]() { using extern OMetaWinxed; }');

  for (string infile in files) {
    var inh = new FileHandle;
    inh.open(infile, 'r');
    string source = inh.readall();
    inh.close();
    var tree      = (new OMetaWinxedParser).matchAll(source, 'topLevel', null, function(var m, int i) {
	say('parse error at index ' + string(i));
	throw m.fail;
    });
    string output = (new OMetaWinxedTranslator).match(tree, 'trans', null, function(var m, int i) {
	say("translation error (shouldn't happen) at index " + string(i));
	throw m.fail;
    });
    outh.print(output);
  }
  outh.close();

  if (execute) {
    var os = getOS();
    var argv = ['winxed'];
    if (opts['lib-path']) {
      string libpath = opts['lib-path'];
      ${ push argv, '-L'    };
      ${ push argv, libpath };
    }
    if (opts['inc-path']) {
      string incpath = opts['inc-path'];
      ${ push argv, '-I'    };
      ${ push argv, incpath };
    }
    ${ push argv, outfile };
    spawnw(argv);
    os.rm(outfile);
  }
}

function getGetopt() {
  using extern Getopt.Obj;
  var getopt = new Getopt.Obj;
  ${ push getopt, 'output|o=s'   }; 
  ${ push getopt, 'lib-path|L=s' }; 
  ${ push getopt, 'inc-path|I=s' }; 
  ${ push getopt, 'help|h'       }; 
  ${ push getopt, 'usage'        }; 
  for (;;)
    yield getopt;
}

function usage_exit() {
  say(
"Usage: Ωη [options] [files]\n" +
"   Available options:\n" +
"     -o --output	  Compile to winxed file\n" +
"     -L --lib-path 	  Add to parrot library search path\n" +
"     -I --inc-path 	  Add to parrot include search path\n" +
"     -h --help --usage	  Print this message\n"
  );

  exit(0);
}

function getOS() {
  var os;
  try {
    loadlib('os');
    os = new 'OS';
  }
  catch() { }
  for (;;)
    yield os;
}

